proceso tateti
    
    // incializa dos matrices de 3x3, una para guardar la ficha que se ve, 
    // y otra para un valor asociado a la ficha, para un jugador sera 1, para
    // el otro 2, entoces para ver quien gano se multiplica por fila, por 
    // columna y por diagonal, si el resultado es 1 gano el primer jugador,
    // si es 8 gano el segundo, si es 0 es porque faltaba completar, si
    // es otra cosa, estan las tres fichas, pero no son del mismo jugador
    definir tab1 como entero;
    definir tab2 como caracter;
    dimension tab1[3,3];
    dimension tab2[3,3];
    definir i,j,cantturnos,valor,pos como entero;
    definir objetivo,aux_i,aux_j,aux_d1,aux_d2 como entero;
    definir terminado,ganador,turnojugador1 como logico;
    definir ficha como caracter;
    para i<-0 hasta 2 hacer
        para j<-0 hasta 2 hacer
            tab1[i,j]<-0;
            tab2[i,j]<-" ";
        finpara
    finpara
    turnojugador1<-verdadero;
    terminado<-falso;
    ganador<-falso;
    cantturnos<-0;
    
    mientras ~ terminado hacer
        
        // dibuja el tablero
        borrar pantalla;
        escribir " ";
        escribir "      ||     ||     ";
        escribir "   ",tab2[0,0],"  ||  ",tab2[0,1],"  ||  ",tab2[0,2];
        escribir "     1||    2||    3";
        escribir " =====++=====++======";
        escribir "      ||     ||     ";
        escribir "   ",tab2[1,0],"  ||  ",tab2[1,1],"  ||  ",tab2[1,2];
        escribir "     4||    5||    6";
        escribir " =====++=====++======";
        escribir "      ||     ||     ";
        escribir "   ",tab2[2,0],"  ||  ",tab2[2,1],"  ||  ",tab2[2,2];
        escribir "     7||    8||    9";
        escribir " ";
        
        si ~ ganador y cantturnos<9 entonces
            
            // carga auxiliares segun a que jugador le toca
            si turnojugador1 entonces
                ficha<-'o'; valor<- 1; objetivo<-1;
                escribir "turno del jugador 1 (x)";
            sino
                ficha<-'x'; valor<- 2; objetivo<-8;
                escribir "turno del jugador 2 (o)";
            finsi
            
            // pide la posicion para colocar la ficha y la valida
            escribir "ingrese la posicion (1-9):";
            
            repetir
                leer pos;
                si pos<1 o pos>9 entonces
                    escribir "posicion incorrecta, ingrese nuevamente: ";
                    pos<-99;
                sino
                    i<-trunc((pos-1)/3);
                    j<-((pos-1) mod 3);
                    si tab1[i,j]<>0 entonces
                        pos<-99;
                        escribir "posicion incorrecta, ingrese nuevamente: ";
                    finsi
                finsi
            hasta que pos<>99
            // guarda la ficha en la matriz tab2 y el valor en tab1
            cantturnos<-cantturnos+1;
            tab1[i,j]<-valor;
            tab2[i,j]<-ficha;
            
            // verifica si gano, buscando que el producto de las fichas en el tablero de objetivo
            aux_d1<-1; aux_d2<-1;
            para i<-0 hasta 2 hacer
                aux_i<-1; aux_j<-1;
                aux_d1<-aux_d1*tab1[i,i];
                aux_d2<-aux_d2*tab1[i,2-i];
                para j<-0 hasta 2 hacer
                    aux_i<-aux_i*tab1[i,j];
                    aux_j<-aux_j*tab1[j,i];
                finpara
                si aux_i=objetivo o aux_j=objetivo entonces
                    ganador<-verdadero;
                finsi
            finpara
            si aux_d1=objetivo o aux_d2=objetivo entonces
                ganador<-verdadero;
            sino
                turnojugador1 <- ~ turnojugador1;
            finsi
            
        sino
            
            si ganador entonces
                escribir "ganador: ";
                si turnojugador1 entonces
                    escribir "jugador 1!";
                sino
                    escribir "jugador 2!";
                finsi
            sino
                escribir "empate!";
            finsi
            terminado<-verdadero;
            
        finsi
        
    finmientras
    
finproceso